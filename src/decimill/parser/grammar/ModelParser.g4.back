
parser grammar ModelParser;

@header {
    package decimill.parser;
    import decimill.expression.*;
    import decimill.model.*;
}

tokens {

    EOL, INLINE, BLOCK, ANY,

    UDIST, NODE, GOAL, ASSM, VAR, NAME, ID, NUM,
    REF, EQ, COMMA, LPR, RPR, PLUS, MINUS, TIMES, DIV, POW, LT, GT, LBR, RBR,
    BNL, BREAK, NL, WS
}

@members {
    Model model;
    TokenStreamRewriter buffer;
}

parse [Model m, TokenStreamRewriter b]

    @init {
        model = m;
        buffer = b;
    }

    : start_line
    | EOF
    ;

start_line
    : BLOCK code_block
    | line
    | EOF
    ;

line
    : ANY line
    | INLINE code_inline BREAK line
    | node_inline line
    | EOL start_line
    | EOF
    ;

code_inline
    : var_dec
    | assignment
    | EOF
    ;

code_block
    :   ( node_dec
            | var_dec
            | refinement
            | assignment
            )
        ( code_block_end
            | BNL code_block
            )
    | BNL code_block
    | code_block_end
    | EOF
    ;

code_block_end
    : NL start_line
    | EOF
    ;

node_inline

    @init {
        Node node;
    }

    : INLINE NODE name = NAME BREAK         { model.addNodeIfNotExists(new Node($name.text.trim())); }
    | INLINE GOAL name = NAME BREAK         { model.addNodeIfNotExists(new Goal($name.text.trim())); }
    | INLINE ASSM name = NAME BREAK         { model.addNodeIfNotExists(new Assumption($name.text.trim())); }
    | in = INLINE name = NAME br = BREAK    { node = model.addNodeIfNotExists(Node.guessFromName($name.text.trim())); buffer.insertBefore($in, "<span class=\"node\" data-modelid=\"" + model.getId() + "\" data-nodeid=\"" + node.getId() + "\">"); buffer.insertAfter($br, "</span>"); }
    ;

node_dec

    : NODE name = NAME                      { model.addNodeIfNotExists(new Node($name.text.trim())); }
    | GOAL name = NAME                      { model.addNodeIfNotExists(new Goal($name.text.trim())); }
    | ASSM name = NAME                      { model.addNodeIfNotExists(new Assumption($name.text.trim())); }
    | name = NAME                           { model.addNodeIfNotExists(Node.guessFromName($name.text.trim())); }
    ;

var_dec
    : VAR? expr_var
        ( COMMA expr_var
        )*
    ;

refinement

    @init {
        Node node = null;
        Node child = null;
    }

    : name = NAME BNL?                      { node = model.addNodeByNameIfNotExists($name.text.trim()); }
        REF BNL? name = NAME                { child = model.addNodeByNameIfNotExists($name.text.trim()); node.addChild(child); }
        ( BNL? COMMA BNL? name = NAME       { child = model.addNodeByNameIfNotExists($name.text.trim()); node.addChild(child); }
        )*
    ;

assignment
    : expr_var EQ BNL? expr_add             { model.assign($expr_var.expr.getId(), $expr_add.expr); model.addVarNameLHS($expr_var.text); }
    ;

expr_add returns [ExprNode expr]
    : expr_sub                              { $expr = $expr_sub.expr; }
        ( BNL? PLUS BNL? expr_sub           { $expr = new PlusNode($expr, $expr_sub.expr); }
        )*
    ;

expr_sub returns [ExprNode expr]
    : expr_mult                             { $expr = $expr_mult.expr; }
        ( BNL? MINUS BNL? expr_mult         { $expr = new MinusNode($expr, $expr_mult.expr); }
        )*
    ;

expr_mult returns [ExprNode expr]
    : expr_div                              { $expr = $expr_div.expr; }
        ( BNL? TIMES BNL? expr_div          { $expr = new TimesNode($expr, $expr_div.expr); }
        )*
    ;

expr_div returns [ExprNode expr]
    : expr_pow                              { $expr = $expr_pow.expr; }
        ( BNL? DIV BNL? expr_pow            { $expr = new DivNode($expr, $expr_pow.expr); }
        )*
    ;

expr_pow returns [ExprNode expr]
    : expr_lt                               { $expr = $expr_lt.expr; }
        ( BNL? POW BNL? expr_lt             { $expr = new PowNode($expr, $expr_lt.expr); }
        )*
    ;

expr_lt returns [ExprNode expr]
    : expr_gt                               { $expr = $expr_gt.expr; }
        ( BNL? LT BNL? expr_gt              { $expr = new LTNode($expr, $expr_gt.expr); }
        )*
    ;

expr_gt returns [ExprNode expr]
    : expr_atom                             { $expr = $expr_atom.expr; }
        ( BNL? GT BNL? expr_atom            { $expr = new GTNode($expr, $expr_atom.expr); }
        )*
    ;

expr_atom returns [ExprNode expr]
    : expr_var                              { $expr = $expr_var.expr; model.addUsedVarName($expr_var.expr.getId()); }
    | constant                              { $expr = $constant.expr; }
    | LPR expr_add RPR                      { $expr = $expr_add.expr; }
    ;

expr_var returns [RefNode expr]
    : id = ID                               { $expr = new RefNode(model.getNamespace(), $id.text); model.addVarName($id.text); }
    ;

constant returns [ExprNode expr]
    : n = NUM                                       { $expr = new DoubleNode(Double.parseDouble($n.text)); }
    | LBR lb = NUM COMMA ub = NUM RBR               { $expr = new Dist90CINode(Double.parseDouble($lb.text), Double.parseDouble($ub.text)); }
    | LBR a = NUM COMMA b = NUM COMMA c = NUM RBR   { $expr = new DistTriangNode(Double.parseDouble($a.text), Double.parseDouble($b.text), Double.parseDouble($c.text)); }
    | LBR a = NUM COMMA b = NUM RBR UDIST           { $expr = new DistUnifNode(Double.parseDouble($a.text), Double.parseDouble($b.text)); }
    ;